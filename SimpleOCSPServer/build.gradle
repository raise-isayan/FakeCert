apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'

repositories {
    mavenCentral()
}

sourceCompatibility = '11' // -source
targetCompatibility = '11' // -target

mainClassName = 'server.ocsp.SimpleJettyServer'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching ('**/*.properties') {
       expand(project.properties)
       // naitive2ascii
       filter(org.apache.tools.ant.filters.EscapeUnicode)
    }
}

clean.doFirst {
    delete fileTree('release') {
      include '*.jar'
    }
}

jar {
    // Keep jar clean:
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF', 'META-INF/**', 'about.html', 'module-info.class'

    manifest {
        attributes 'Main-Class': mainClassName
    }

    from {
        configurations.runtimeClasspath.filter{ it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
    }

    destinationDirectory = file('release')
    archiveVersion =  "v${release_version_major}"
}

task release(type: Zip, dependsOn: ['build']) {
    archiveBaseName ="${rootProject.name}_v${release_version_major}.${release_version_minor}"
    destinationDirectory = file("${projectDir}")
    from rootProject.rootDir
    include '*'
    include 'gradle/**'
    include 'image/**'
    include 'libs/**'
    include 'src/**'
    include 'release/**'
    exclude 'build'
    exclude '.git'
    exclude '.gradle'
    exclude '*.zip'
}

dependencies {
    // https://github.com/raise-isayan/BurpExtensionCommons
    implementation fileTree(dir: 'libs', include: ['*.jar'])
        // https://mvnrepository.com/artifact/com.google.code.gson/gson
        implementation 'com.google.code.gson:gson:2.9.0'
        // https://mvnrepository.com/artifact/com.googlecode.juniversalchardet/juniversalchardet
        implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'

    // https://mvnrepository.com/artifact/net.portswigger.burp.extender/burp-extender-api
    compileOnly 'net.portswigger.burp.extender:burp-extender-api:2.3'
    // https://mvnrepository.com/artifact/org.eclipse.jetty/jetty-server
    implementation 'org.eclipse.jetty:jetty-server:9.4.48.v20220622'
    // https://mvnrepository.com/artifact/org.bouncycastle/bcpkix-jdk15on
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.64'
    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    implementation 'org.bouncycastle:bcprov-jdk15on:1.64'

    // UnitTest
    testImplementation fileTree(dir: 'libs', include: ['*.jar'])
    // https://mvnrepository.com/artifact/net.portswigger.burp.extender/burp-extender-api
    testImplementation 'net.portswigger.burp.extender:burp-extender-api:2.3'
    // https://mvnrepository.com/artifact/org.eclipse.jetty/jetty-client
    testImplementation 'org.eclipse.jetty:jetty-client:9.4.48.v20220622'
    testImplementation     'junit:junit:4.13.2'

}
